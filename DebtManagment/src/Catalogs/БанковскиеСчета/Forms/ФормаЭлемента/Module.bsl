
#Область ОписаниеПеременных

&НаКлиенте
Перем ИмяРедактируемогоРеквизита;

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Обработка оповещения выбора банка
// после ввода БИК.
//
// Параметры:
//  Ответ - Строка - ответ.
//  ДополнительныеПараметры - Структура - структура дополнительных параметров.
//
&НаКлиенте
Процедура ОповещениеСохраненияИзменений(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = "ОтменитьВвод" Тогда
		БИКБанка = "";
		ЗаполнитьРеквизитыБанкаПоБИК("Банк", БИКБанка, Истина);
	ИначеЕсли Ответ = "ПродолжитьВвод" Тогда
		Объект.РучноеИзменениеРеквизитовБанка = Истина;
		Объект.БИКБанка = БИКБанка;
	ИначеЕсли Ответ = "ВыбратьИзСписка" Тогда
        
        // &ЗамерПроизводительности
        ОценкаПроизводительностиРТКлиент.НачатьЗамер(
                 Истина, "Справочник.КлассификаторБанков.Форма.ФормаВыбора.Открытие");

        СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("Реквизит", "БИКБанка");
		ОткрытьФорму("Справочник.КлассификаторБанков.Форма.ФормаВыбора", СтруктураПараметров, ЭтаФорма);
	КонецЕсли;
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//ор_ПрограммноеСозданиеЭлементовФорм.СоздатьЭлементыФормы(ЭтаФорма);
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		БИКБанка          = "";
		НаименованиеБанка = "";
		КоррСчетБанка     = "";
		ГородБанка        = "";
		
		Если Объект.РучноеИзменениеРеквизитовБанка Тогда
			БИКБанка          = Объект.БИКБанка;
			НаименованиеБанка = Объект.НаименованиеБанка;
			КоррСчетБанка     = Объект.КоррСчетБанка;
			ГородБанка        = Объект.ГородБанка;
		Иначе
			Если НЕ Объект.Банк.Пустая() Тогда
				ЗаполнитьРеквизитыБанкаПоБанку("Банк", Объект.Банк, Ложь);
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			Если Объект.РучноеИзменениеРеквизитовБанка Тогда
				БИКБанка          = Объект.БИКБанка;
				НаименованиеБанка = Объект.НаименованиеБанка;
				КоррСчетБанка     = Объект.КоррСчетБанка;
				ГородБанка        = Объект.ГородБанка;
			Иначе
				Если НЕ Объект.Банк.Пустая() Тогда
					ЗаполнитьРеквизитыБанкаПоБанку("Банк", Объект.Банк, Ложь);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли;
	
	УправлениеЭлементамиФормы();
	
	СформироватьАвтоНаименование();
	
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(РезультатВыбора, ИсточникВыбора)

	Если (ИсточникВыбора.ИмяФормы = "Справочник.БанковскиеСчета.Форма.РеквизитыБанка") Тогда
		Если Не ПустаяСтрока(РезультатВыбора) Тогда
			Если РезультатВыбора.Реквизит = "БИКБанка" Тогда
				Объект.РучноеИзменениеРеквизитовБанка = РезультатВыбора.РучноеИзменение;
				Если РезультатВыбора.РучноеИзменение Тогда
					Объект.Банк              = "";
					Объект.БИКБанка          = РезультатВыбора.ЗначенияПолей.БИК;
					Объект.НаименованиеБанка = РезультатВыбора.ЗначенияПолей.Наименование;
					Объект.КоррСчетБанка     = РезультатВыбора.ЗначенияПолей.КоррСчет;
					Объект.ГородБанка        = РезультатВыбора.ЗначенияПолей.Город;
					Объект.АдресБанка        = РезультатВыбора.ЗначенияПолей.Адрес;
					Объект.ТелефоныБанка     = РезультатВыбора.ЗначенияПолей.Телефоны;
					
					БИКБанка          = РезультатВыбора.ЗначенияПолей.БИК;
					КоррСчетБанка     = РезультатВыбора.ЗначенияПолей.КоррСчет;
					НаименованиеБанка = РезультатВыбора.ЗначенияПолей.Наименование;
					ГородБанка        = РезультатВыбора.ЗначенияПолей.Город;
				Иначе
					Объект.Банк              = РезультатВыбора.Банк;
					Объект.БИКБанка          = "";
					Объект.НаименованиеБанка = "";
					Объект.КоррСчетБанка     = "";
					Объект.ГородБанка        = "";
					Объект.АдресБанка        = "";
					Объект.ТелефоныБанка     = "";
					
					ЗаполнитьРеквизитыБанкаПоБанку("Банк", Объект.Банк, Ложь);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли (ИсточникВыбора.ИмяФормы = "Справочник.КлассификаторБанков.Форма.ФормаВыбора") Тогда	
		Если ТипЗнч(РезультатВыбора) = Тип("СправочникСсылка.КлассификаторБанков") Тогда
			Если ИмяРедактируемогоРеквизита = "БИКБанка" Тогда
				Объект.Банк              = РезультатВыбора;
				Объект.БИКБанка          = "";
				Объект.НаименованиеБанка = "";
				Объект.КоррСчетБанка     = "";
				Объект.ГородБанка        = "";
				Объект.АдресБанка        = "";
				Объект.ТелефоныБанка     = "";

				ЗаполнитьРеквизитыБанкаПоБанку("Банк", РезультатВыбора, Ложь);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Окно <> Неопределено Тогда
		Окно.Активизировать();
	КонецЕсли;
	
	УправлениеЭлементамиФормы();
	СформироватьАвтоНаименование();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	//ор_ПрограммноеСозданиеЭлементовФорм.СоздатьЭлементыФормы(ЭтаФорма);
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	УправлениеЭлементамиФормы();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Объект.РучноеИзменениеРеквизитовБанка Тогда
		Если БИКБанка <> Объект.БИКБанка Тогда
			Объект.БИКБанка = БИКБанка;
		КонецЕсли;
		Если КоррСчетБанка <> Объект.КоррСчетБанка Тогда
			Объект.КоррСчетБанка = КоррСчетБанка;
		КонецЕсли;
		Если НаименованиеБанка <> Объект.НаименованиеБанка Тогда
			Объект.НаименованиеБанка = НаименованиеБанка;
		КонецЕсли;
		Если ГородБанка <> Объект.ГородБанка Тогда
			Объект.ГородБанка = ГородБанка;
		КонецЕсли;
	КонецЕсли;
	
	Если ПустаяСтрока(Объект.Наименование) Тогда
		Объект.Наименование = СформироватьАвтоНаименование();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ ЗначениеЗаполнено(Объект.Владелец) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Нстр("ru='Создать банковский счет возможно только из формы владельца счета либо копированием уже существующего.'"),  , , , Отказ);
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РучноеИзменениеРеквизитовБанкаПриИзменении(Элемент)
	ЗаписьОБанке = "";
	Если Объект.РучноеИзменениеРеквизитовБанка Тогда
		Если НЕ Объект.Банк.Пустая() Тогда
			ЗаполнитьРеквизитыБанкаПоБанку("Банк", Объект.Банк, Истина);
		КонецЕсли;
		Объект.Банк = "";
	Иначе
		ЗаполнитьРеквизитыБанкаПоБИК("Банк", Объект.БИКБанка, Истина);
		Объект.БИКБанка          = "";
		Объект.НаименованиеБанка = "";
		Объект.КоррСчетБанка     = "";
		Объект.ГородБанка        = "";
		Объект.АдресБанка        = "";
		Объект.ТелефоныБанка     = "";
	КонецЕсли;
	
	УправлениеЭлементамиФормы();

КонецПроцедуры

&НаКлиенте
Процедура БИКБанкаПриИзменении(Элемент)
	
	ИмяРедактируемогоРеквизита = "БИКБанка";
	РеквизитБанкаПриИзменении("БИКБанка");
	
КонецПроцедуры

&НаКлиенте
Процедура БИКБанкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИмяРедактируемогоРеквизита = "БИКБанка";
	РеквизитБанкаПриВыборе("БИКБанка", ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура БИКБанкаОткрытие(Элемент, СтандартнаяОбработка)
	ИмяРедактируемогоРеквизита = "БИКБанка";
	СтандартнаяОбработка = Ложь;
	РеквизитБанкаОткрытие("БИКБанка");
КонецПроцедуры

&НаКлиенте
Процедура НомерСчетаПриИзменении(Элемент)
	СформироватьАвтоНаименование();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция формирует наименование банковского счета.
//
&НаСервере
Функция СформироватьАвтоНаименование()
	
	Элементы.Наименование.СписокВыбора.Очистить();
	
	Если Объект.РучноеИзменениеРеквизитовБанка Тогда
		СтрокаНаименования = Прав(СокрЛП(Объект.НомерСчета), 4) 
		+ ?(ЗначениеЗаполнено(Объект.НаименованиеБанка), " в " + Строка(Объект.НаименованиеБанка), "")
		+ ", " + СокрЛП(Объект.Владелец);
		СтрокаНаименования = Лев(СтрокаНаименования, 150);
		
		Элементы.Наименование.СписокВыбора.Добавить(СтрокаНаименования);
		
		СтрокаНаименования = ?(ЗначениеЗаполнено(Объект.НаименованиеБанка), Строка(Объект.НаименованиеБанка), "")
		+ ", " + СокрЛП(объект.Владелец);
		СтрокаНаименования = Лев(СтрокаНаименования, 150);
		
		Элементы.Наименование.СписокВыбора.Добавить(СтрокаНаименования);
		
	Иначе
		
		СтрокаНаименования = Прав(СокрЛП(Объект.НомерСчета), 4) 
		+ ?(ЗначениеЗаполнено(Объект.Банк), " в " + Строка(Объект.Банк), "")
		+ ", " + СокрЛП(Объект.Владелец);
		СтрокаНаименования = Лев(СтрокаНаименования, 150);
		
		Элементы.Наименование.СписокВыбора.Добавить(СтрокаНаименования);
		
		СтрокаНаименования = ?(ЗначениеЗаполнено(Объект.Банк), Строка(Объект.Банк), "")
		+ ", " + СокрЛП(объект.Владелец);
		СтрокаНаименования = Лев(СтрокаНаименования, 150);
		
		Элементы.Наименование.СписокВыбора.Добавить(СтрокаНаименования);
		
	КонецЕсли;
	
	Возврат СтрокаНаименования;

КонецФункции

&НаКлиенте
Процедура РеквизитБанкаПриВыборе(ИмяЭлемента, Форма)
	Если ИмяЭлемента = "БИКБанка" Тогда
        Если Не Объект.РучноеИзменениеРеквизитовБанка Тогда
            
            // &ЗамерПроизводительности
            ОценкаПроизводительностиРТКлиент.НачатьЗамер(
                     Истина, "Справочник.КлассификаторБанков.Форма.ФормаВыбора.Открытие");

			СтруктураПараметров = Новый Структура;
			СтруктураПараметров.Вставить("Реквизит", ИмяЭлемента);
			ОткрытьФорму("Справочник.КлассификаторБанков.Форма.ФормаВыбора", СтруктураПараметров, Форма);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РеквизитБанкаОткрытие(ИмяЭлемента)
	
    // &ЗамерПроизводительности
	ОценкаПроизводительностиРТКлиент.НачатьЗамер(
		         Истина, "Справочник.БанковскиеСчета.Форма.РеквизитыБанка.Открытие");
                 
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Реквизит", ИмяЭлемента);
	ЗначенияПараметров = Новый Структура;
	
	Если ИмяЭлемента = "БИКБанка" Тогда
		
		СтруктураПараметров.Вставить("РучноеИзменение", Объект.РучноеИзменениеРеквизитовБанка);
		
		Если Объект.РучноеИзменениеРеквизитовБанка Тогда
			ЗначенияПараметров.Вставить("БИК", БИКБанка);
			ЗначенияПараметров.Вставить("Наименование", НаименованиеБанка);
			ЗначенияПараметров.Вставить("КоррСчет", КоррСчетБанка);
			ЗначенияПараметров.Вставить("Город", ГородБанка);
			ЗначенияПараметров.Вставить("Адрес", Объект.АдресБанка);
			ЗначенияПараметров.Вставить("Телефоны", Объект.ТелефоныБанка);
		Иначе
			СтруктураПараметров.Вставить("Банк", Объект.Банк);
		КонецЕсли;
		
	КонецЕсли;
	
	СтруктураПараметров.Вставить("ЗначенияПолей", ЗначенияПараметров);
	ОткрытьФорму("Справочник.БанковскиеСчета.Форма.РеквизитыБанка",СтруктураПараметров, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитБанкаПриИзменении(ИмяЭлемента)

	Если (ИмяЭлемента = "БИКБанка") И Не Объект.РучноеИзменениеРеквизитовБанка Тогда
		
		Если Не ЗаполнитьРеквизитыБанкаПоБИК("Банк", БИКБанка, Истина) Тогда
			СписокВариантовОтветовНаВопрос = Новый СписокЗначений;
			СписокВариантовОтветовНаВопрос.Добавить("ВыбратьИзСписка", НСтр("ru = 'Выбрать из списка'"));
			СписокВариантовОтветовНаВопрос.Добавить("ПродолжитьВвод",  НСтр("ru = 'Продолжить ввод'"));
			СписокВариантовОтветовНаВопрос.Добавить("ОтменитьВвод",	   НСтр("ru = 'Отменить ввод'"));
			
			ТекстВопроса = НСтр("ru = 'Банк с БИК  %Значение% не найден в классификаторе банков.'");
			ТекстВопроса = СтрЗаменить(ТекстВопроса,"%Значение%",БИКБанка);
			
			ОписаниеОповещения = Новый ОписаниеОповещения("ОповещениеСохраненияИзменений", ЭтотОбъект);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, СписокВариантовОтветовНаВопрос, 0 ,,НСтр("ru = 'Выбор банка из классификатора'"));
			
		Иначе
			УправлениеЭлементамиФормы();
		КонецЕсли;
		
	Иначе
		УправлениеЭлементамиФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьРеквизитыБанкаПоБИК(ТипБанка, БИК = "", ПеренестиЗначенияРеквизитов = Ложь)
	
	НашлиПоБИК   = Ложь;
	ЗаписьОБанке = "";
	БИК = СокрЛП(БИК);
	
	Если ТипБанка = "Банк" Тогда
		БИКБанка          = "";
		КоррСчетБанка     = "";
		НаименованиеБанка = "";
		ГородБанка        = "";
		
		Если БИК <> "" Тогда
			РаботаСБанками.ПолучитьДанныеКлассификатора(БИК,,ЗаписьОБанке);
		Иначе
			ЗаписьОБанке = Справочники.КлассификаторБанков.ПустаяСсылка();
		КонецЕсли;

		Если ТипЗнч(ЗаписьОБанке) = Тип("СправочникСсылка.КлассификаторБанков") Тогда
			БИКБанка          = ЗаписьОБанке.Код;
			КоррСчетБанка     = ЗаписьОБанке.КоррСчет;
			НаименованиеБанка = ЗаписьОБанке.Наименование;
			ГородБанка        = ЗаписьОБанке.Город;
			НашлиПоБИК        = Истина;
			Если ПеренестиЗначенияРеквизитов Тогда
				Объект.БИКБанка          = "";
				Объект.НаименованиеБанка = "";
				Объект.КоррСчетБанка     = "";
				Объект.ГородБанка        = "";
				Объект.АдресБанка        = "";
				Объект.ТелефоныБанка     = "";
				Объект.Банк              = ЗаписьОБанке;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат НашлиПоБИК;
	
КонецФункции

&НаСервере
Функция ЗаполнитьРеквизитыБанкаПоБанку(ТипБанка, Банк = "", ПеренестиЗначенияРеквизитов = Ложь)
	Если ТипЗнч(Банк) = Тип("СправочникСсылка.КлассификаторБанков") Тогда
		Если ТипБанка = "Банк" Тогда
			БИКБанка          = Банк.Код;
			КоррСчетБанка     = Банк.КоррСчет;
			НаименованиеБанка = Банк.Наименование;
			ГородБанка        = Банк.Город;
			Если ПеренестиЗначенияРеквизитов Тогда
				Объект.БИКБанка          = Банк.Код;
				Объект.НаименованиеБанка = Банк.Наименование;
				Объект.КоррСчетБанка     = Банк.КоррСчет;
				Объект.ГородБанка        = Банк.Город;
				Объект.АдресБанка        = Банк.Адрес;
				Объект.ТелефоныБанка     = Банк.Телефоны;
				Объект.Банк              = "";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура УправлениеЭлементамиФормы()
	
	РучноеИзменение = Объект.РучноеИзменениеРеквизитовБанка;
	
	// Установим использование банка для расчетов.
	Элементы.КоррСчетБанка.ТолькоПросмотр     = НЕ РучноеИзменение;
	Элементы.НаименованиеБанка.ТолькоПросмотр = НЕ РучноеИзменение;
	Элементы.ГородБанка.ТолькоПросмотр        = НЕ РучноеИзменение;
	
	Элементы.БИКБанка.КнопкаВыбора = НЕ РучноеИзменение;
	Элементы.БИКБанка.КнопкаОчистки = РучноеИзменение;
	
КонецПроцедуры

#КонецОбласти
