#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользоватьКомиссионнуюТорговлю = ПолучитьФункциональнуюОпцию("ИспользоватьКомиссионнуюТорговлю");
	Список.Параметры.УстановитьЗначениеПараметра("ИспользоватьКомиссионнуюТорговлю", ИспользоватьКомиссионнуюТорговлю);
	
	ЗапросПоПравам();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
    
    // &ЗамерПроизводительности
    ОценкаПроизводительностиРТКлиент.НачатьЗамер(
		     Истина, "Справочник.ДоговорыКонтрагентов.Форма.ФормаСписка.Открытие");

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияОтборПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(
		Список,
		"Организация",
		Организация,
		ЗначениеЗаполнено(Организация));
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговораСОшибкамиОтборПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(
		Список,
		"ДоговорСОшибкой",
		ДоговораСОшибками,
		ДоговораСОшибками);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийСписка

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
    
    // &ЗамерПроизводительности
	ОценкаПроизводительностиРТКлиент.НачатьЗамер(
		     Истина, "Справочник.ДоговорыКонтрагентов.Форма.ФормаЭлемента.Открытие");

КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
    
    // &ЗамерПроизводительности
	ОценкаПроизводительностиРТКлиент.НачатьЗамер(
		     Истина, "Справочник.ДоговорыКонтрагентов.Форма.ФормаЭлемента.СозданиеНового");

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗапросПоПравам()
	
	ЕстьПраво = ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ТоварыОрганизаций);
	
	Если НЕ ЕстьПраво ИЛИ НЕ ИспользоватьКомиссионнуюТорговлю Тогда
		Список.ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СправочникДоговорыКонтрагентов.Ссылка КАК Ссылка,
		|	СправочникДоговорыКонтрагентов.ПометкаУдаления КАК ПометкаУдаления,
		|	СправочникДоговорыКонтрагентов.Наименование КАК Наименование,
		|	СправочникДоговорыКонтрагентов.Организация КАК Организация,
		|	СправочникДоговорыКонтрагентов.БанковскийСчет КАК БанковскийСчет,
		|	СправочникДоговорыКонтрагентов.ТипДоговора КАК ТипДоговора,
		|	СправочникДоговорыКонтрагентов.Агент КАК Агент,
		|	СправочникДоговорыКонтрагентов.Посредник КАК Посредник,
		|	СправочникДоговорыКонтрагентов.ПроцентКомиссионногоВознаграждения КАК ПроцентКомиссионногоВознаграждения,
		|	СправочникДоговорыКонтрагентов.УслугаКомиссионногоВознаграждения КАК УслугаКомиссионногоВознаграждения,
		|	СправочникДоговорыКонтрагентов.СпособРасчетаКомиссионногоВознаграждения КАК СпособРасчетаКомиссионногоВознаграждения,
		|	СправочникДоговорыКонтрагентов.АдресОператораПеревода КАК АдресОператораПеревода,
		|	СправочникДоговорыКонтрагентов.ИННОператораПеревода КАК ИННОператораПеревода,
		|	СправочникДоговорыКонтрагентов.НаименованиеОператораПеревода КАК НаименованиеОператораПеревода,
		|	СправочникДоговорыКонтрагентов.ОперацияПлатежногоАгента КАК ОперацияПлатежногоАгента,
		|	СправочникДоговорыКонтрагентов.БанковскийПлатежныйАгент КАК БанковскийПлатежныйАгент,
		|	СправочникДоговорыКонтрагентов.ТелефонПлатежногоАгента КАК ТелефонПлатежногоАгента,
		|	СправочникДоговорыКонтрагентов.ТелефонОператораПоПриемуПлатежей КАК ТелефонОператораПоПриемуПлатежей,
		|	СправочникДоговорыКонтрагентов.ТелефонОператораПеревода КАК ТелефонОператораПеревода,
		|	СправочникДоговорыКонтрагентов.ТелефонПоставщика КАК ТелефонПоставщика,
		|	СправочникДоговорыКонтрагентов.ИННПоставщикаУслуг КАК ИННПоставщикаУслуг,
		|	СправочникДоговорыКонтрагентов.ПризнакАгента КАК ПризнакАгента,
		|	СправочникДоговорыКонтрагентов.УчитыватьНДС КАК УчитыватьНДС,
		|	СправочникДоговорыКонтрагентов.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
		|	СправочникДоговорыКонтрагентов.СтатусДействия КАК СтатусДействия,
		|	СправочникДоговорыКонтрагентов.УчетНДСУКомитента КАК УчетНДСУКомитента,
		|	СправочникДоговорыКонтрагентов.СтавкаНДС КАК СтавкаНДС,
		|	СправочникДоговорыКонтрагентов.Предопределенный КАК Предопределенный,
		|	СправочникДоговорыКонтрагентов.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных,
		|	ВЫБОР
		|		КОГДА СправочникДоговорыКонтрагентов.УчетНДСУКомитента = ЗНАЧЕНИЕ(Перечисление.УчетНДСУКомитента.ВыделятьНДСЗаКомиссионныеТоварыПриПечатиЧека)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ВыделятьНДС,
		|	ЛОЖЬ КАК ДоговорСОшибкой
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК СправочникДоговорыКонтрагентов";
		
		Элементы.ДоговораСОшибкамиОтбор.Видимость = Ложь;
		
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
