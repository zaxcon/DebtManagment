
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	
	СформироватьАвтоНаименование(ЭтотОбъект);
	Если НЕ Объект.Наименование = Элементы.Наименование.СписокВыбора.Получить(0) Тогда
		НаименованиеИзмененоПользователем = Истина;
	КонецЕсли;
	
	УстановитьВидимостьДоступность(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьАвтонаименование();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	Если Ожидание = 0 Тогда
		СформироватьАвтоНаименование(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	НаименованиеИзмененоПользователем = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	Объект.Наименование = Текст;
	НаименованиеИзмененоПользователем = ЗначениеЗаполнено(Текст);
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Элементы.БанковскийСчет.Доступность = ЗначениеЗаполнено(Объект.Организация);
	ОбновитьАвтонаименование()
	
КонецПроцедуры

&НаКлиенте
Процедура ПризнакАгентаПриИзменении(Элемент)
	
	ОбнулитьПроцентКомиссионногоВознаграждения();
	
	УстановитьВидимостьДоступность(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура АгентПриИзменении(Элемент)
	
	ОбновитьАвтонаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура СпособРасчетаВознагражденияПриИзменении(Элемент)
	
	УстановитьВидимостьДоступность(ЭтотОбъект);
	ОбнулитьПроцентКомиссионногоВознаграждения();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастроитьПроцентАгентскогоВознагражденияНажатие(Элемент)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОткрытьФормуСтавкиАгентскогоВознаграждения();
	Иначе
		
		ТекстВопроса = НСтр("ru='Перед настройкой необходимо записать объект. Записать?'");
		
		ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеВопросЗаписатьПередНастройкойВознаграждения", ЭтотОбъект);
		ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АвтоматическиВыделятьВознаграждениеВЧекеПриИзменении(Элемент)
	УстановитьВидимостьДоступность(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьДоступность(Форма)
	
	ПризнакАгента = Форма.Объект.ПризнакАгента;
	
	ЭтоБанковскийПлатежныйАгент = ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.БанковскийПлатежныйАгент")
		ИЛИ ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.БанковскийПлатежныйСубагент");
	ЭтоПлатежныйАгент           = ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.ПлатежныйАгент")
		ИЛИ ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.ПлатежныйСубагент");
	ЭтоКомиссионер              = ПризнакАгента = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.Комиссионер");
	
	// Установим видимость.
	Форма.Элементы.ГруппаКомитент.Видимость                             = ЭтоКомиссионер;
	Форма.Элементы.ГруппаАгентскоеВознаграждение.Видимость              = НЕ ЭтоКомиссионер;
	Форма.Элементы.ГруппаРеквизитыПлатежногоАгента.Видимость            = НЕ ЭтоКомиссионер;
	Форма.Элементы.ГруппаПараметрыБанковскогоПлатежногоАгента.Видимость = ЭтоБанковскийПлатежныйАгент;
	
	// Установим доступность.
	Форма.Элементы.БанковскийСчет.Доступность = ЗначениеЗаполнено(Форма.Объект.Организация);
	Форма.Элементы.ГруппаАвтоматическоеВыделениеАгентскогоВознаграждения.Доступность = Форма.Объект.АвтоматическиВыделятьВознаграждениеВЧеке;
	
	Форма.Элементы.ПроцентКомиссионногоВознаграждения.Доступность = НЕ Форма.Объект.СпособРасчетаКомиссионногоВознаграждения = 
		ПредопределенноеЗначение("Перечисление.СпособыРасчетаКомиссионногоВознаграждения.НеРассчитывается");
		
	Если ЭтоКомиссионер Тогда
		ТекстЗаголовка = Нстр("ru='Комитент'");
	Иначе
		ТекстЗаголовка = Нстр("ru='Поставщик услуг'");
	КонецЕсли;

	Форма.Элементы.Агент.Заголовок = ТекстЗаголовка;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СформироватьАвтоНаименование(Форма)
	
	Форма.Элементы.Наименование.СписокВыбора.Очистить();
	СтрокаНаименования = Строка(Форма.Объект.Агент) + " (" + Строка(Форма.Объект.Организация) + ")";
	Форма.Элементы.Наименование.СписокВыбора.Добавить(СтрокаНаименования);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьАвтонаименование()
	
	СформироватьАвтоНаименование(ЭтотОбъект);
	Если НЕ ЗначениеЗаполнено(Объект.Наименование) ИЛИ НЕ НаименованиеИзмененоПользователем Тогда
		Объект.Наименование = Элементы.Наименование.СписокВыбора.Получить(0);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбнулитьПроцентКомиссионногоВознаграждения()
	
	Если Элементы.ПроцентКомиссионногоВознаграждения.Доступность = Ложь
		И НЕ ЭтотОбъект.ТолькоПросмотр Тогда
		Объект.ПроцентКомиссионногоВознаграждения = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеВопросЗаписатьПередНастройкойВознаграждения(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		Если Записать() Тогда
			ОткрытьФормуСтавкиАгентскогоВознаграждения();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуСтавкиАгентскогоВознаграждения()
	Отбор          = Новый Структура("ДоговорКонтрагента", Объект.Ссылка);
	ПараметрыФормы = Новый Структура("Отбор", Отбор);
	
	ОткрытьФорму("РегистрСведений.ПроцентыВознагражденияПоДоговорам.ФормаСписка", ПараметрыФормы, ЭтотОбъект);
КонецПроцедуры

#КонецОбласти