////////////////////////////////////////////////////////////////////////////////
// ОтборыСписковКлиентСервер содержит процедуры и функции 
// для работы с отборами списков.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Добавление группы отбора.
//
// Параметры:
//  КоллекцияЭлементов - ГруппаЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора.
//  ТипГруппы - ТипГруппыЭлементовОтбораКомпоновкиДанных - тип группы.
//  Представление - Строка - представление группы.
//
// Возвращаемое значение:
//  ГруппаЭлементовОтбораКомпоновкиДанных - группа элементов отбора данных.
//
Функция СоздатьГруппуЭлементовОтбора(КоллекцияЭлементов, Представление, ТипГруппы) Экспорт
	
	ГруппаЭлементовОтбора = НайтиЭлементОтбораПоПредставлению(КоллекцияЭлементов, Представление);
	Если ГруппаЭлементовОтбора = Неопределено Тогда
		ГруппаЭлементовОтбора = КоллекцияЭлементов.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	Иначе
		ГруппаЭлементовОтбора.Элементы.Очистить();
	КонецЕсли;
	
	ГруппаЭлементовОтбора.Представление    = Представление;
	ГруппаЭлементовОтбора.Применение       = ТипПримененияОтбораКомпоновкиДанных.Элементы;
	ГруппаЭлементовОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ГруппаЭлементовОтбора.ТипГруппы        = ТипГруппы;
	ГруппаЭлементовОтбора.Использование    = Истина;
	
	Возврат ГруппаЭлементовОтбора;
	
КонецФункции

// Изменяет элемент отбора в группе элементов отбора.
//
// Параметры:
//  Группа - ГруппаЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора.
//  ИмяПоля - Строка - наименование поля компоновки данных.
//  ПравоеЗначение - ПравоеЗначение - значение, которое нужно установить.
//  Установить - Булево - признак необходимости установить отбор.
//  ВидСравнения - ВидСравнения - вид сравнения отбора, по умолчанию - Равно.
//
Процедура ИзменитьЭлементОтбораГруппыСписка(Группа, ИмяПоля, ПравоеЗначение = Неопределено, Установить = Ложь, ВидСравнения = Неопределено) Экспорт
	
	УдалитьЭлементОтбораКоллекции(Группа.Элементы, ИмяПоля);
	
	Если Установить Тогда
		УстановитьЭлементОтбораКоллекции(Группа.Элементы, ИмяПоля, ПравоеЗначение, ВидСравнения);
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает элемент отбора в группе элементов отбора.
//
// Параметры:
//  Группа - ГруппаЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора.
//  ИмяПоля - Строка - наименование поля компоновки данных.
//  ПравоеЗначение - ПравоеЗначение - значение, которое нужно установить.
//  Установить - Булево - признак необходимости установить отбор.
//  ВидСравнения - ВидСравнения - вид сравнения отбора, по умолчанию - Равно.
//
Процедура УстановитьЭлементОтбораГруппыСписка(Группа, ИмяПоля, ПравоеЗначение = Неопределено, Установить = Ложь, ВидСравнения = Неопределено) Экспорт
	
	Если Установить Тогда
		УстановитьЭлементОтбораКоллекции(Группа.Элементы, ИмяПоля, ПравоеЗначение, ВидСравнения);
	Иначе
		УдалитьЭлементОтбораКоллекции(Группа.Элементы, ИмяПоля);
	КонецЕсли;
	
КонецПроцедуры // ИзменитьЭлементОтбораСписка()

// Копирует элементы отбора в группе элементов отбора.
//
// Параметры:
//  ГруппаЭлементов - ГруппаЭлементовОтбораКомпоновкиДанных - группа элементов приемник.
//  КоллекцияЭлементовИсточник - ГруппаЭлементовОтбораКомпоновкиДанных - группа элементов источник данных.
//  ПроверятьДоступность - Булево - признак проверки доступности элементов.
//  ОчищатьПриемник - Булево - признак очищения приемника значений.
// 
Процедура СкопироватьЭлементыОтбораВГруппуСписка(ГруппаЭлементов, КоллекцияЭлементовИсточник, ПроверятьДоступность = Ложь, ОчищатьПриемник = Истина) Экспорт
	
	ГруппаЭлементов.Элементы.Очистить();
	
	Для каждого ЭлементКоллекции Из КоллекцияЭлементовИсточник Цикл
		ЗаполнитьЗначенияСвойств(ГруппаЭлементов.Элементы.Добавить(ТипЗнч(ЭлементКоллекции)), ЭлементКоллекции);
	КонецЦикла;
	
КонецПроцедуры

// Удаляет элемент отбора динамического списка.
//
// Параметры:
// Список - ДинамическийСписок - обрабатываемый динамический список,
// ИмяПоля - Строка - имя поля компоновки, отбор по которому нужно удалить.
//
Процедура УдалитьЭлементОтбораСписка(Список, ИмяПоля) Экспорт
	
	УдалитьЭлементОтбораКоллекции(Список.КомпоновщикНастроек.Настройки.Отбор.Элементы, ИмяПоля);
	
КонецПроцедуры // УдалитьЭлементОтбораСписка()

// Устанавливает элемент отбор динамического списка.
//
// Параметры:
//  Список - ДинамическийСписок - обрабатываемый динамический список.
//  ИмяПоля - Строка - наименование поля компоновки данных.
//  ВидСравнения - ВидСравнения - вид сравнения отбора, по умолчанию - Равно.
//  ПравоеЗначение - ПравоеЗначение - значение, которое нужно установить.
//
Процедура УстановитьЭлементОтбораСписка(Список, ИмяПоля, ПравоеЗначение, ВидСравнения = Неопределено) Экспорт
	
	УстановитьЭлементОтбораКоллекции(Список.КомпоновщикНастроек.Настройки.Отбор.Элементы, ИмяПоля, ПравоеЗначение, ВидСравнения);
	
КонецПроцедуры

// Изменяет элемент отбора динамического списка.
//
// Параметры:
//  Список - ДинамическийСписок - обрабатываемый динамический список.
//  ИмяПоля - Строка - наименование поля компоновки данных.
//  ПравоеЗначение - ПравоеЗначение - значение, которое нужно установить.
//  Установить - Булево - признак необходимости установить отбор.
//  ВидСравнения - ВидСравнения - вид сравнения отбора, по умолчанию - Равно.
//
Процедура ИзменитьЭлементОтбораСписка(Список, ИмяПоля, ПравоеЗначение = Неопределено, Установить = Ложь, ВидСравнения = Неопределено) Экспорт
	
	УдалитьЭлементОтбораСписка(Список, ИмяПоля);
	
	Если Установить Тогда
		УстановитьЭлементОтбораСписка(Список, ИмяПоля, ПравоеЗначение, ВидСравнения);
	КонецЕсли;
	
КонецПроцедуры // ИзменитьЭлементОтбораСписка()

// Устанавливает отбор в списке по указанному значению для нужной колонки
// с учетом переданной структуры быстрого отбора и переданных настроек
//
// Параметры:
//  Список - ДинамическийСписок - динамический список, для которого требуется установить отбор.
//  ИмяКолонки - Строка - имя колонки, по которой устанавливается отбор.
//  Значение - Произвольный - устанавливаемое значение отбора.
//  СтруктураБыстрогоОтбора - Неопределено, Структура - структура, содержащая ключи и значения отбора.
//  Настройки - НастройкиКомпоновкиДанных - настройки, из которых могут получаться значения отбора.
//  Использование - Неопределено, Булево - признак использования элемента отбора.
//  ВидСравнения - Неопределено, ВидСравненияКомпоновкиДанных - вид сравнения, устанавливаемый для элемента отбора.
//  ПриводитьЗначениеКЧислу - Булево - признак приведения значения к числу.
//
Процедура ОтборПоЗначениюСпискаПриЗагрузкеИзНастроек(Список, ИмяКолонки, Значение, Знач СтруктураБыстрогоОтбора, 
			Настройки, Использование = Неопределено, ВидСравнения = Неопределено, ПриводитьЗначениеКЧислу = Ложь) Экспорт
	
	Если СтруктураБыстрогоОтбора = Неопределено Тогда
		Значение = Настройки.Получить(ИмяКолонки);
		Если ПриводитьЗначениеКЧислу Тогда
			Значение = ?(ЗначениеЗаполнено(Значение), Число(Значение), Значение);
		КонецЕсли;
		ИспользованиеЭлементаОтбора = ?(Использование = Неопределено, ЗначениеЗаполнено(Значение), Использование);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, ИмяКолонки, Значение, ВидСравнения,,ИспользованиеЭлементаОтбора);
	Иначе
		Если Не СтруктураБыстрогоОтбора.Свойство(ИмяКолонки) Тогда
			Значение = Настройки.Получить(ИмяКолонки);
			Если ПриводитьЗначениеКЧислу Тогда
				Значение = ?(ЗначениеЗаполнено(Значение), Число(Значение), Значение);
			КонецЕсли;
			ИспользованиеЭлементаОтбора = ?(Использование = Неопределено, ЗначениеЗаполнено(Значение), Использование);
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, ИмяКолонки, Значение, ВидСравнения,,ИспользованиеЭлементаОтбора);
		КонецЕсли;
	КонецЕсли;
	
	Настройки.Удалить(ИмяКолонки);
	
КонецПроцедуры // ОтборПоЗначениюСпискаПриЗагрузкеИзНастроек()

// Устанавливает отбор в списке по указанному значению для нужной колонки
// с учетом переданной структуры быстрого отбора
//
// Параметры:
//  Список - ДинамическийСписок - динамический список, для которого требуется установить отбор.
//  ИмяКолонки - Строка - имя колонки, по которой устанавливается отбор.
//  Значение - Произвольный - устанавливаемое значение отбора.
//  СтруктураБыстрогоОтбора - Неопределено, Структура - структура, содержащая ключи и значения отбора.
//  Использование - Неопределено, Булево - признак использования элемента отбора.
//  ВидСравнения - Неопределено, ВидСравненияКомпоновкиДанных - вид сравнения, устанавливаемый для элемента отбора.
//  ПриводитьЗначениеКЧислу - Булево - признак приведения значения к числу.
//
Процедура ОтборПоЗначениюСпискаПриСозданииНаСервере(Список, ИмяКолонки, Значение, Знач СтруктураБыстрогоОтбора, 
			Использование = Неопределено, ВидСравнения = Неопределено, ПриводитьЗначениеКЧислу = Ложь) Экспорт
	
	Если СтруктураБыстрогоОтбора <> Неопределено Тогда
		
		Если СтруктураБыстрогоОтбора.Свойство(ИмяКолонки, Значение) Тогда
			Если ПриводитьЗначениеКЧислу Тогда
				Значение = ?(ЗначениеЗаполнено(Значение), Число(Значение), Значение);
			КонецЕсли;
			ИспользованиеЭлементаОтбора = ?(Использование = Неопределено, ЗначениеЗаполнено(Значение), Использование);
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, ИмяКолонки, Значение, ВидСравнения,,ИспользованиеЭлементаОтбора);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ОтборПоЗначениюСпискаПриСозданииНаСервере()

// Устанавливает значение Значение параметра ИмяПараметра динамического списка Список 
// или отключает его использование в зависимости от признака Использование.
//
// Параметры:
//  Список - ДинамическийСписок - динамический список, для которого требуется установить отбор.
//  ИмяПараметра - Строка - имя колонки, по которой устанавливается отбор.
//  Значение - Произвольный - устанавливаемое значение отбора.
//  Использование - Неопределено, Булево - признак использования элемента отбора.
//
Процедура УстановитьПараметрДинамическогоСписка(Список, ИмяПараметра, Значение, Использование = Истина) Экспорт
	
	Если Использование Тогда
		ЗначениеПараметра = Список.Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
		Если ЗначениеПараметра <> Неопределено Тогда
			Список.Параметры.УстановитьЗначениеПараметра(ИмяПараметра, Значение);
		КонецЕсли;
	Иначе
		
		ЗначениеПараметра = Список.Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
		Если ЗначениеПараметра <> Неопределено Тогда
			Если ЗначениеПараметра.Значение <> Значение Тогда
				ЗначениеПараметра.Значение = Значение;
			КонецЕсли;
			Если ЗначениеПараметра.Использование <> Использование Тогда
				ЗначениеПараметра.Использование = Использование;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Поиск элементов и групп отбора динамического списка. Поиск может осуществляться или по ЛевомуЗначению, или по Представлению.
//
// Параметры:
// ОбластьПоиска - ОтборКомпоновкиДанных - контейнер с элементами и группами отбора.
// ИмяПоля - Строка - имя поля компоновки (не используется для групп).
// Представление - Строка - представление поля компоновки.
//
Функция НайтиЭлементыИГруппыОтбора(Знач ОбластьПоиска,
									Знач ИмяПоля = Неопределено,
									Знач Представление = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ИмяПоля) Тогда
		ЗначениеПоиска = Новый ПолеКомпоновкиДанных(ИмяПоля);
		СпособПоиска = 1;
	Иначе
		СпособПоиска = 2;
		ЗначениеПоиска = Представление;
	КонецЕсли;
	
	МассивЭлементов = Новый Массив;
	
	НайтиРекурсивно(ОбластьПоиска.Элементы, МассивЭлементов, СпособПоиска, ЗначениеПоиска);
	
	Возврат МассивЭлементов;
	
КонецФункции

#Если ТонкийКлиент ИЛИ ВебКлиент ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ТолстыйКлиентУправляемоеПриложение Тогда

// Процедура предназначена для открытия формы выбора справочника ВидыОплатЧекаККМ с установленным отбором на список
// выбора.
//
// Параметры:
//  СтруктураПараметров - Структура - параметры выполнения операции.
//  СтандартнаяОбработка - Булево - признак стандартной обработки.
//
Процедура НачалоВыбораВидаОплатыЧекаККМ(СтруктураПараметров, СтандартнаяОбработка) Экспорт
	Перем	ПараметрПравоеЗначение,
			ПараметрВладелец,
			ИмяПоляОтбораЛевоеЗначение;
	
	ПараметрПравоеЗначение = СтруктураПараметров.ПравоеЗначение;
	
	Если ТипЗнч(ПараметрПравоеЗначение) = Тип("СписокЗначений") Тогда
		Если ПараметрПравоеЗначение.Количество() >= 1 Тогда
			СтандартнаяОбработка = Ложь;
		Иначе
			СтандартнаяОбработка = Истина;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ПараметрПравоеЗначение) = Тип("ПеречислениеСсылка.ТипыОплатЧекаККМ") Тогда
		СтандартнаяОбработка = Ложь;
	Иначе
		СтандартнаяОбработка = Истина;
	КонецЕсли;
	
	Если СтандартнаяОбработка Тогда
		Возврат;
	Иначе
		ПараметрВладелец  = СтруктураПараметров.Владелец;
		ИмяПоляОтбораЛевоеЗначение = СтруктураПараметров.ИмяПоляОтбораЛевоеЗначение;
		
		ЭквайринговыйТерминал = СтруктураПараметров.ЭквайринговыйТерминал;
		СтруктураПараметров = Новый Структура();
        
        // &ЗамерПроизводительности
	    ОценкаПроизводительностиРТКлиент.НачатьЗамер(
		         Истина, "Справочник.ВидыОплатЧекаККМ.Форма.ФормаВыбора.Открытие");

		СтруктураПараметровОтбора = Новый Структура; 
		СтруктураПараметровОтбора.Вставить("ИмяПоляОтбораЛевоеЗначение", ИмяПоляОтбораЛевоеЗначение);
		СтруктураПараметровОтбора.Вставить("ПравоеЗначение", ПараметрПравоеЗначение);
		СтруктураПараметровОтбора.Вставить("Ссылка", ЭквайринговыйТерминал);
		СтруктураПараметровОтбора.Вставить("ЭквайринговыйТерминал", ЭквайринговыйТерминал);
		
		СтруктураПараметров.Вставить("СтруктураПараметрыОтбора", СтруктураПараметровОтбора);
		ОткрытьФорму("Справочник.ВидыОплатЧекаККМ.Форма.ФормаВыбора", СтруктураПараметров, ПараметрВладелец);
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура НайтиРекурсивно(КоллекцияЭлементов, МассивЭлементов, СпособПоиска, ЗначениеПоиска)
	
	Для каждого ЭлементОтбора Из КоллекцияЭлементов Цикл
		
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			
			Если СпособПоиска = 1 Тогда
				Если ЭлементОтбора.ЛевоеЗначение = ЗначениеПоиска Тогда
					МассивЭлементов.Добавить(ЭлементОтбора);
				КонецЕсли;
			ИначеЕсли СпособПоиска = 2 Тогда
				Если ЭлементОтбора.Представление = ЗначениеПоиска Тогда
					МассивЭлементов.Добавить(ЭлементОтбора);
				КонецЕсли;
			КонецЕсли;
		Иначе
			
			НайтиРекурсивно(ЭлементОтбора.Элементы, МассивЭлементов, СпособПоиска, ЗначениеПоиска);
			
			Если СпособПоиска = 2 И ЭлементОтбора.Представление = ЗначениеПоиска Тогда
				МассивЭлементов.Добавить(ЭлементОтбора);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьЭлементОтбораКоллекции(КоллекцияЭлементов, ИмяПоля)
	
	ПолеКомпоновки = Новый ПолеКомпоновкиДанных(ИмяПоля);
	Для Каждого ЭлементОтбора Из КоллекцияЭлементов Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных")
			И ЭлементОтбора.ЛевоеЗначение = ПолеКомпоновки Тогда
			КоллекцияЭлементов.Удалить(ЭлементОтбора);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // УдалитьЭлементОтбораСписка()

Функция НайтиЭлементОтбораПоПредставлению(КоллекцияЭлементов, Представление)
	
	ВозвращаемоеЗначение = Неопределено;
	
	Для каждого ЭлементОтбора Из КоллекцияЭлементов Цикл
		Если ЭлементОтбора.Представление = Представление Тогда
			ВозвращаемоеЗначение = ЭлементОтбора;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение
	
КонецФункции

// Устанавливает элемент отбора коллекции.
//
// Параметры:
//  КоллекцияЭлементов - ГруппаЭлементовОтбораКомпоновкиДанных - контейнер с элементами и группами отбора.
//  ИмяПоля - Строка - наименование поля компоновки данных.
//  ПравоеЗначение - ПравоеЗначение - значение, которое нужно установить.
//  ВидСравнения - ВидСравнения - вид сравнения отбора, по умолчанию - Равно.
//
Процедура УстановитьЭлементОтбораКоллекции(КоллекцияЭлементов, ИмяПоля, ПравоеЗначение, ВидСравнения = Неопределено)
	
	ЭлементОтбора = КоллекцияЭлементов.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение    = Новый ПолеКомпоновкиДанных(ИмяПоля);
	ЭлементОтбора.ВидСравнения     = ?(ВидСравнения = Неопределено, ВидСравненияКомпоновкиДанных.Равно, ВидСравнения);
	ЭлементОтбора.Использование    = Истина;
	ЭлементОтбора.ПравоеЗначение   = ПравоеЗначение;
	ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
КонецПроцедуры // УстановитьЭлементОтбораСписка()

#КонецОбласти

